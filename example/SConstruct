opts = Variables()

opts.Add(EnumVariable('target', 'Set target', 'linux',
					allowed_values=('linux','macosx'),
					map = {},
					ignorecase=2))

# Add opencv builder to tools
tools_list = [
	# Add default tools
	'default',
	'opencvBuilder'
]

# Init and export build environment
env = Environment(variables = opts, tools = tools_list)
Export('env')

env['BUILD_TARGET'] = env['target']

# Opencv builder settings
env['openCV_DIR'] = Dir('opencv/opencv').abspath
env['OPENCVBUILDER_INCLUDE_DIR'] = Dir('include').abspath
env['OPENCVBUILDER_LIBS_DIR'] = Dir('lib').abspath

# Compilation settings
env['CPPFLAGS'] = []
env['CPPPATH'] = []
env['CPPFLAGS'].append('-Ofast')
env['CPPPATH'].append(env['OPENCVBUILDER_INCLUDE_DIR'])

# Define a variant dir
buildDir = 'build'
opencvBuildDir = '{buildDir}/build_opencv'.format(buildDir = buildDir)
VariantDir(buildDir, '.')
VariantDir(opencvBuildDir, 'opencv')

# Define SConscript files
SConscript_files = [
	'{opencvBuildDir}/SConscript'.format(opencvBuildDir = opencvBuildDir),
	'{buildDir}/SConscript'.format(buildDir = buildDir)
]

SConscript(SConscript_files)
